pkgname=ps5-payload-sdl2_kitchensink
pkgver=1.0.12
pkgrel=1
pkgdesc="FFmpeg and SDL2 based library for audio and video playback, written in C99."
arch=('any')
url="https://katajakasa.github.io/SDL_kitchensink/"
license=('MIT')
source=("https://github.com/katajakasa/SDL_kitchensink/archive/refs/tags/$pkgver.tar.gz")
sha256sums=('e9b5406b68434cd50fa209aacbf8aefe83e76e9feacfaaf306e018a56b333970')
options=(!strip libtool staticlibs)
groups=('ps5-payload-dev')
makedepends=('ps5-payload-sdk' 'git')
depends=('ps5-payload-sdl2' 'ps5-payload-ffmpeg' 'ps5-payload-libass')

prepare() {
    if [ -z "${PS5_PAYLOAD_SDK}" ]; then
	export PS5_PAYLOAD_SDK=/opt/ps5-payload-sdk
    fi
}

build() {
    source "${PS5_PAYLOAD_SDK}/toolchain/prospero.sh"
    cd SDL_kitchensink-$pkgver

    ${CMAKE} -DCMAKE_BUILD_TYPE=Release \
	     -DBUILD_STATIC=YES \
	     -DBUILD_SHARED=NO \
	     -B build \
             -S .
    ${MAKE} -C build
}

package() {
    source "${PS5_PAYLOAD_SDK}/toolchain/prospero.sh"
    cd SDL_kitchensink-$pkgver

    export DESTDIR="${pkgdir}/${DESTDIR}"
    ${MAKE} -C build install
}
