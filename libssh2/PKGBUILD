pkgname=ps5-payload-libssh2
pkgver=1.11.1
pkgrel=1
pkgdesc="A library implementing the SSH2 protocol as defined by Internet Drafts"
url="https://www.libssh2.org/"
arch=('any')
license=('BSD')
options=(!strip libtool staticlibs)
source=("https://www.libssh2.org/download/libssh2-$pkgver.tar.gz")
sha256sums=('d9ec76cbe34db98eec3539fe2c899d26b0c837cb3eb466a56b0f109cabf658f7')
makedepends=('ps5-payload-sdk')
depends=('ps5-payload-libressl' 'ps5-payload-zlib')
groups=('ps5-payload-dev')

prepare() {
    if [ -z "${PS5_PAYLOAD_SDK}" ]; then
	export PS5_PAYLOAD_SDK=/opt/ps5-payload-sdk
    fi
}

build() {
    source "${PS5_PAYLOAD_SDK}/toolchain/prospero.sh"
    cd libssh2-$pkgver

    ./configure --prefix="${PREFIX}" --host=x86_64-pc-freebsd \
		--enable-static --disable-shared \
		--disable-docker-tests
    ${MAKE} V=1
}

package() {
    source "${PS5_PAYLOAD_SDK}/toolchain/prospero.sh"
    cd libssh2-$pkgver

    export DESTDIR="${pkgdir}/${DESTDIR}"
    ${MAKE} install

    ${PS5_CROSS_FIX_ROOT} "${DESTDIR}/${PREFIX}"
}

