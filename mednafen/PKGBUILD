pkgname=ps5-payload-mednafen
pkgver=1.32.1
pkgrel=1
pkgdesc='A portable multi-system emulator'
arch=('any')
url='https://mednafen.github.io/'
license=('GPLv2')
options=(!strip libtool staticlibs)
source=("https://mednafen.github.io/releases/files/mednafen-$pkgver.tar.xz")
sha256sums=('de7eb94ab66212ae7758376524368a8ab208234b33796625ca630547dbc83832')
makedepends=('ps5-payload-sdk' 'ps5-payload-sdl2' 'ps5-payload-flac')
groups=('ps5-payload-dev' 'ps5-payload-game')

prepare() {
    if [ -z "${PS5_PAYLOAD_SDK}" ]; then
	export PS5_PAYLOAD_SDK=/opt/ps5-payload-sdk
    fi

    cd mednafen

    # The SDK libs includes both pthread_setname_np() and pthread_set_name_np(),
    # but only have headers for pthread_set_name_np(). Mednafen will default
    # to pthread_setname_np(), which casuses compiler errors due to missing
    # declaration in headers.
    sed -i \
	's|defined(HAVE_PTHREAD_SETNAME_NP)|0|g' \
	src/mthreading/MThreading_POSIX.cpp
}

build() {
    source "${PS5_PAYLOAD_SDK}/toolchain/prospero.sh"
    cd mednafen

    ./configure --prefix="${PREFIX}" --host=x86_64-pc-freebsd \
		--without-libiconv-prefix \
		--disable-debugger --disable-nls
    ${MAKE}
}

package() {
    source "${PS5_PAYLOAD_SDK}/toolchain/prospero.sh"
    cd mednafen

    export DESTDIR="$pkgdir/${DESTDIR}"
    ${MAKE} install
}
