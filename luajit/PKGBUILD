pkgname=ps5-payload-luajit
pkgver=2.1.1748459687
pkgrel=1
pkgdesc='Just-in-time compiler and drop-in replacement for Lua 5.1'
arch=('any')
url='https://luajit.org/'
license=('MIT')
options=(!strip libtool staticlibs)
source=("git+https://github.com/LuaJIT/LuaJIT.git#commit=f9140a622a0c44a99efb391cc1c2358bc8098ab7")
sha256sums=('SKIP')
groups=('ps5-payload-dev')
makedepends=('git' 'ps5-payload-sdk' 'ps5-payload-openlibm' 'ps5-payload-libcxx')

prepare() {
    if [ -z "${PS5_PAYLOAD_SDK}" ]; then
	export PS5_PAYLOAD_SDK=/opt/ps5-payload-sdk
    fi

    cd LuaJIT
    # LuaJIT actually supports the ps5, but will disable
    # ffi which the payload sdk supports.
    sed -i 's|__PROSPERO__|__JUNK__|g' src/lj_arch.h
}


build() {
    source "${PS5_PAYLOAD_SDK}/toolchain/prospero.sh"
    # Build wants normal clang in path too
    export PATH="/usr/lib/llvm18/bin:$PATH"
    cd LuaJIT

    ${MAKE} amalg BUILDMODE=static \
	    CROSS="${PS5_PAYLOAD_SDK}/bin/prospero-" \
	    DEFAULT_CC="clang" TARGET_LIBS="-lunwind" Q=
}

package() {
    source "${PS5_PAYLOAD_SDK}/toolchain/prospero.sh"
    cd LuaJIT

    export DESTDIR="${pkgdir}/${DESTDIR}"
    ${MAKE} install PREFIX="${PREFIX}"

    ${PS5_CROSS_FIX_ROOT} "${DESTDIR}/${PREFIX}"
}
