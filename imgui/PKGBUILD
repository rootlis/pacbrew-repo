pkgname=ps5-payload-imgui
pkgver=1.91.0
pkgrel=1
pkgdesc="Bloat-free Graphical User interface for C++"
license=('MIT')
arch=('any')
url="https://github.com/ocornut/imgui"

# On each update, keep up to date with latest sha in https://github.com/microsoft/vcpkg/commits/master/ports/imgui
_vcpkg_sha=f7a371bbd9945c9059875492ba018ec12cb925c2
source=("$pkgname-$pkgver.tar.gz::https://codeload.github.com/ocornut/imgui/tar.gz/v${pkgver}"
        "CMakeLists.v${pkgver}-${pkgrel}.txt::https://raw.githubusercontent.com/microsoft/vcpkg/${_vcpkg_sha}/ports/imgui/CMakeLists.txt"
        "imgui-config.v${pkgver}-${pkgrel}.cmake.in::https://raw.githubusercontent.com/microsoft/vcpkg/${_vcpkg_sha}/ports/imgui/imgui-config.cmake.in")
sha256sums=('6e62c87252e6b3725ba478a1c04dc604aa0aaeec78fedcf4011f1e52548f4cc9'
            'f1d4a017e97f2a43ee4f79ceba7a7c26c068bc29703e8d3a937193b7729260c5'
            'dc7580735db811d67e61fc3a37037f72aabc36c23d074ab98e670b00ad3f4a97')

options=(!strip libtool staticlibs)
groups=('ps5-payload-dev')
depends=('ps5-payload-sdl2')
makedepends=('ps5-payload-sdk')

prepare() {
    if [ -z "${PS5_PAYLOAD_SDK}" ]; then
	export PS5_PAYLOAD_SDK=/opt/ps5-payload-sdk
    fi

    cp CMakeLists.v${pkgver}-${pkgrel}.txt        imgui-${pkgver}/CMakeLists.txt
    cp imgui-config.v${pkgver}-${pkgrel}.cmake.in imgui-${pkgver}/imgui-config.cmake.in
}

build() {
    source "${PS5_PAYLOAD_SDK}/toolchain/prospero.sh"
    cd imgui-$pkgver

    ${CMAKE} -DCMAKE_BUILD_TYPE=Release \
	     -DIMGUI_BUILD_SDL2_BINDING=YES \
	     -DIMGUI_BUILD_SDL2_RENDERER_BINDING=YES \
	     -B build \
             -S .
    ${MAKE} -C build
}

package() {
    source "${PS5_PAYLOAD_SDK}/toolchain/prospero.sh"
    cd imgui-$pkgver

    export DESTDIR="${pkgdir}/${DESTDIR}"
    ${MAKE} -C build install

    ${PS5_CROSS_FIX_ROOT} "${DESTDIR}/${PREFIX}"
}
