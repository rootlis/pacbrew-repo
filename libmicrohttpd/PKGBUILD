pkgname=ps5-payload-libmicrohttpd
pkgver=1.0.1
pkgrel=1
pkgdesc='a small C library that is supposed to make it easy to run an HTTP server as part of another application.'
arch=('any')
url='https://www.gnu.org/software/libmicrohttpd/'
license=('LGPL')
source=("https://ftp.gnu.org/gnu/libmicrohttpd/libmicrohttpd-$pkgver.tar.gz")
sha256sums=('a89e09fc9b4de34dde19f4fcb4faaa1ce10299b9908db1132bbfa1de47882b94')
options=(!strip libtool staticlibs)
makedepends=('ps5-payload-sdk')
groups=('ps5-payload-dev')

prepare() {
    if [ -z "${PS5_PAYLOAD_SDK}" ]; then
	export PS5_PAYLOAD_SDK=/opt/ps5-payload-sdk
    fi
}

build() {
    source "${PS5_PAYLOAD_SDK}/toolchain/prospero.sh"
    cd libmicrohttpd-${pkgver}

    ./configure --prefix="${PREFIX}" --host=x86_64-pc-freebsd \
		--enable-static --disable-shared --disable-doc \
		--disable-curl --disable-examples
    ${MAKE}
}


package() {
    source "${PS5_PAYLOAD_SDK}/toolchain/prospero.sh"
    cd libmicrohttpd-${pkgver}

    export DESTDIR="${pkgdir}/${DESTDIR}"
    ${MAKE} install

    ${PS5_CROSS_FIX_ROOT} "${DESTDIR}/${PREFIX}"
}
