pkgname=ps5-payload-file
pkgver=5.45
pkgrel=1
pkgdesc='File type identification utility'
arch=('any')
license=('custom')
url='https://www.darwinsys.com/file/'
options=(!strip libtool staticlibs)
source=("https://astron.com/pub/file/file-$pkgver.tar.gz")
sha256sums=('fc97f51029bb0e2c9f4e3bffefdaf678f0e039ee872b9de5c002a6d09c784d82')
makedepends=('ps5-payload-sdk')
depends=('ps5-payload-zlib' 'ps5-payload-xz' 'ps5-payload-bzip2')
groups=('ps5-payload-dev')

prepare() {
    if [ -z "${PS5_PAYLOAD_SDK}" ]; then
	export PS5_PAYLOAD_SDK=/opt/ps5-payload-sdk
    fi
}

build() {
    cd file-$pkgver

    # Build host binary first
    mkdir host-build
    pushd host-build
    ../configure --prefix="$(pwd)/staging" \
		--enable-static --disable-shared
    make
    make install
    popd

    # Use host binary to cross-build
    export PATH="$(pwd)/host-build/staging/bin:$PATH"
    source "${PS5_PAYLOAD_SDK}/toolchain/prospero.sh"
    ./configure --prefix="${PREFIX}" --host=x86_64-pc-freebsd \
		--enable-static --disable-shared
    ${MAKE}
}

package() {
    source "${PS5_PAYLOAD_SDK}/toolchain/prospero.sh"
    cd file-$pkgver

    export DESTDIR="${pkgdir}/${DESTDIR}"
    make install

    ${PS5_CROSS_FIX_ROOT} "${DESTDIR}/${PREFIX}"
}
