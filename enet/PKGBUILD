pkgname=ps5-payload-enet
pkgver=1.3.18
pkgrel=1
pkgdesc='A relatively thin, simple and robust network communication layer on top of UDP'
arch=('any')
url='http://enet.bespin.org/'
license=('custom')
options=(!strip libtool staticlibs)
source=("http://enet.bespin.org/download/enet-${pkgver}.tar.gz")
sha256sums=('2a8a0c5360d68bb4fcd11f2e4c47c69976e8d2c85b109dd7d60b1181a4f85d36')

groups=('ps5-payload-dev')
makedepends=('ps5-payload-sdk')


prepare() {
    if [ -z "${PS5_PAYLOAD_SDK}" ]; then
	export PS5_PAYLOAD_SDK=/opt/ps5-payload-sdk
    fi
}

build() {
    source "${PS5_PAYLOAD_SDK}/toolchain/prospero.sh"
    cd enet-${pkgver}

    ./configure --prefix="${PREFIX}" --host=x86_64-pc-freebsd \
		--disable-shared --enable-static 
    ${MAKE}
}

package() {
    source "${PS5_PAYLOAD_SDK}/toolchain/prospero.sh"
    cd enet-${pkgver}

    export DESTDIR="${pkgdir}/${DESTDIR}"
    make install

    ${PS5_CROSS_FIX_ROOT} "${DESTDIR}/${PREFIX}"
}
